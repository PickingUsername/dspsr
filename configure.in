#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([DSPSR],[1.0],[straten@sourceforge.net])

AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([base/TimeSeries.h])

AC_ARG_VAR([PSRHOME],[Standard pulsar home directory])
AC_ARG_VAR([LOGIN_ARCH],[Architecture-dependent sub-directory of PSRHOME])
AC_PREFIX_DEFAULT([${PSRHOME:-"/usr/local"}${PSRHOME:+"/$LOGIN_ARCH"}])

if test x"$PSRHOME" = x; then
  AC_MSG_WARN([The PSRHOME environment variable is not set])
else
  if test x"$LOGIN_ARCH" = x; then
    AC_MSG_WARN([The LOGIN_ARCH environment variable is not set])
  fi
fi

# Check if the user has set compiler options
SWIN_OPTIONS_SET

# Enable convenience compiler selection
SWIN_COMPILER

AM_INIT_AUTOMAKE([subdir-objects])

# Create static libraries by default
AC_DISABLE_SHARED

# Enable large-file support
AC_SYS_LARGEFILE

# Determine the machine endian
AC_C_BIGENDIAN

# Checks for programs.
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_CC
AC_PROG_CPP
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_FC
AC_F77_WRAPPERS
#_STAR_RESTFP_FIX

#
# Disable the debugging information option, -g
#
SWIN_DEBUG

#
# Set up to use the fink package installation directory
#
if test -d /sw; then
  CPPFLAGS="-I/sw/include $CPPFLAGS"
  LDFLAGS="-L/sw/lib $LDFLAGS"
else
  #
  # Set up to use the standard package installation directory
  #
  if test -d /usr/local; then
    CPPFLAGS="-I/usr/local/include $CPPFLAGS"
    LDFLAGS="-L/usr/local/lib $LDFLAGS"
  fi
fi

if test "$PSRHOME"; then
 CPPFLAGS="-I$PSRHOME/packages/$LOGIN_ARCH/include $CPPFLAGS"
 LDFLAGS="-L$PSRHOME/packages/$LOGIN_ARCH/lib $LDFLAGS"
fi

#
# Checks for essential libraries
#
AC_CHECK_LIB([m], [sin])
SWIN_LIB_PSRCHIVE([],AC_MSG_ERROR([PSRCHIVE Library not found.

Please see http://psrchive.sourceforge.net

]))

#
# Checks for optional libraries
#
SWIN_LIB_MPI

#
# Checks for graphics libraries
#
SWIN_LIB_PGPLOT

#
# For developers: preserve file modification times
#
INSTALL_DATA="${INSTALL} -m 644 -p"
install_sh="CPPROG='cp -p' ${install_sh}"

#
# Checks for header files.
#
AC_CHECK_HEADERS([malloc.h])

#
# Checks for library functions.
#
SWIN_FUNC_GETOPT_LONG

AM_CONFIG_HEADER([config.h])
AC_CONFIG_FILES([Makefile])

AC_OUTPUT

rm -f config/Makefile.local

